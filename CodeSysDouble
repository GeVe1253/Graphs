FUNCTION_BLOCK FB_OvenZone
VAR_INPUT
    Enable          : BOOL := TRUE;
    Setpoint        : REAL := 85.0;
    
    // COUPLING INPUT: Represents the valve position of the upstream radiator
    // For Zone 1, this will be 0.
    // For Zone 2, this will be Zone 1's Valve Position.
    Coupling_In     : REAL := 0.0; 
    
    // How much does the upstream valve hurt me?
    // 0.41 means if upstream is 100% open, I lose 41% of my power.
    Coupling_Ratio  : REAL := 0.41; 
END_VAR
VAR_OUTPUT
    Valve_Pos       : REAL;
    Slave_SP        : REAL;
	Oven_Temp       : REAL;
END_VAR
VAR
    // Controllers
    MasterPID       : PID;
    SlavePID        : PID;
    
    // Physics State
    Raw_Air_Temp    : REAL;
    Dtime           : REAL := 2.0; // Task
    SimSpeed        : REAL := 10.0;
    
    // Helpers
    Effective_Valve : REAL;
    Max_Change      : REAL;
    T2_Seconds      : REAL;
    Actual_K_Gain   : REAL := 0.8125;
    Tau_2           : TIME := T#1169s;
END_VAR





// ---------------------------------------------------------
// 1. PID PARAMETER ASSIGNMENT (Time-Scaled)
// ---------------------------------------------------------

MasterPID.KP   := 5.6;
MasterPID.TN   := 1200.0 / SimSpeed; 
MasterPID.TV   := 150.0 / SimSpeed;  
MasterPID.Y_MIN := 0.0;
MasterPID.Y_MAX := 100.0;

SlavePID.KP    := 0.8;
SlavePID.TN    := 600.0 / SimSpeed;
SlavePID.TV    := 0.0;
SlavePID.Y_MIN := 0.0;
SlavePID.Y_MAX := 100.0;


// ---------------------------------------------------------
// 2. CONTROL LOOP (INDEPENDENT CASCADES)
// ---------------------------------------------------------

// The Master PID doesn't know about the other zone. 
// It just sees that the temp is dropping and asks for more heat.
MasterPID(ACTUAL := Oven_Temp, SET_POINT := Setpoint);
Slave_SP := MasterPID.Y;

SlavePID(ACTUAL := Valve_Pos, SET_POINT := Slave_SP);


// ---------------------------------------------------------
// 3. PHYSICS ENGINE (WITH UPSTREAM STARVATION)
// ---------------------------------------------------------

// A. Actuator Movement (Ramp)
Max_Change := 1.36 * SimSpeed * Dtime;

IF (SlavePID.Y > Valve_Pos) THEN
    Valve_Pos := MIN(Valve_Pos + Max_Change, SlavePID.Y);
ELSIF (SlavePID.Y < Valve_Pos) THEN
    Valve_Pos := MAX(Valve_Pos - Max_Change, SlavePID.Y);
END_IF;

Valve_Pos := LIMIT(0.0, Valve_Pos, 100.0);

// B. Calculate "Effective" Heat
// Upstream - Downstream Mechanics
Effective_Valve := Valve_Pos - (Coupling_In * Coupling_Ratio);
Effective_Valve := MAX(0.0, Effective_Valve);

// C. Thermal Lag Application
T2_Seconds := TO_REAL(TIME_TO_DINT(Tau_2)) / 1000.0;

IF (T2_Seconds + (Dtime * SimSpeed)) > 0 THEN
    Raw_Air_Temp := Raw_Air_Temp + 
        ((Effective_Valve * Actual_K_Gain - Raw_Air_Temp) * ((Dtime * SimSpeed) / (T2_Seconds + (Dtime * SimSpeed))));
END_IF;

Oven_Temp := Raw_Air_Temp + 20.0;
